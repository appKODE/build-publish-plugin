import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// plugins block in groovy script doesn't support using "alias(..)"
// see https://github.com/gradle/gradle/issues/15383#issuecomment-1396061277
// but outside of plugins block everything works ok
plugins {
  id "org.jetbrains.kotlin.jvm"
  id "io.gitlab.arturbosch.detekt"
  id "org.jlleitschuh.gradle.ktlint"
}

kotlin {
  jvmToolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(KotlinCompile).configureEach {
  compilerOptions {
    jvmTarget.set(JvmTarget.JVM_17)
    freeCompilerArgs.add("-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi")
    freeCompilerArgs.add("-opt-in=kotlinx.coroutines.FlowPreview")
    freeCompilerArgs.add("-opt-in=kotlin.contracts.ExperimentalContracts")
    freeCompilerArgs.add("-Xopt-in=kotlin.ExperimentalStdlibApi")
  }
}

ktlint {
  verbose = true
  outputToConsole = true
  enableExperimentalRules = true
  debug = false
  android = false
  ignoreFailures = false
  disabledRules = ["import-ordering"]
  filter {
    include("src/main/**/*.kt")
  }
}

detekt {
  source = files(
    "src/main/java",
    "src/main/kotlin"
  )
  config = files(rootProject.file("../config/detekt/detekt.yml"))
  ignoredBuildTypes = [ "release" ]
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
  jvmTarget = "17"
  // "exclude(**/generated/**)" doesn't work for some reason, see
  // https://github.com/detekt/detekt/issues/4127#issuecomment-1260733842
  exclude {
    // Use separator to make sure it also works on Windows
    def sep = File.separator
    it.file.absolutePath.contains("${sep}generated${sep}")
  }
  reports {
    html.required = true
    xml.required = false
    txt.required = false
    sarif.required = false
    html.outputLocation = file("build/reports/detekt.html")
  }
}

tasks.withType(io.gitlab.arturbosch.detekt.DetektCreateBaselineTask).configureEach {
  jvmTarget = "17"
}
